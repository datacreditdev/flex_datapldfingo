<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="398" creationPolicy="all" xmlns:control="CONTROL.*">
	<mx:states>
		<mx:State name="moral">
			<mx:SetProperty target="{lblTitulo}" name="text" value="Domicilio de Oficinas"/>
			<mx:AddChild relativeTo="{lblCodPostal}" position="before">
				<mx:Label id="lblNumInt" text="Número Interior:" x="61.5" y="104"/>
			</mx:AddChild>
			<mx:AddChild position="lastChild">
				<mx:Label id="lblNumExt" text="Número Exterior:" x="57.5" y="74"/>
			</mx:AddChild>
			<mx:AddChild position="lastChild">
				<mx:TextInput id="txtNumExt" maxChars="20" x="150.5" y="73" width="170"/>
			</mx:AddChild>
			<mx:AddChild position="lastChild">
				<mx:TextInput id="txtNumInt" maxChars="20" x="150.5" y="103" width="170"/>
			</mx:AddChild>
			<mx:SetProperty target="{lblEntre}" name="y" value="322"/>
			<mx:SetProperty target="{txtEntreCalles}" name="y" value="321"/>
			<mx:RemoveChild target="{lblTelefono}"/>
		</mx:State>
	</mx:states>
	<mx:Metadata>
		[Event(name="enviarDatosAcred", type="Data.EventAcred")]
	</mx:Metadata>
	
	<mx:Validator id="calleV" source="{txtCalle}" property="text" 
	invalid="{txtCalle.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Calle Requerido"/>
	
	<mx:Validator id="codPostalV" source="{txtCodPostal}" property="text" 
	invalid="{txtCodPostal.setFocus()}" triggerEvent="" requiredFieldError="Código Postal Requerido"/>
	
	<mx:Validator id="entFedV" source="{comEntGrid.txtDato}" property="text" 
	invalid="{comEntGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Entidad Federativa Requerido"/>
	
	<mx:Validator id="municipioV" source="{comMunGrid.txtDato}" property="text" 
	invalid="{comMunGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Municipio Requerido"/>
	
	<mx:Validator id="localidadV" source="{comLocGrid.txtDato}" property="text" 
	invalid="{comLocGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Localidad Requerido"/>
	
	<mx:Validator id="coloniaV" source="{comColGrid.txtDato}" property="text" 
	invalid="{comColGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Colonia Requerido"/>

	<mx:Script>
		<![CDATA[
			import Data.DatosAcred;
			import Data.EventAcred;
			import Data.Globales;
			import Data.Utils;
			import mx.collections.ArrayCollection;
			import mx.containers.TitleWindow;
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.effects.easing.Elastic;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.soap.WebService;
			import mx.utils.StringUtil;
			import mx.validators.Validator;
			
			private var _xmlDir:XML =  new XML();
			private var _xmlColonia:XML =  new XML();
			private var _xmlPais:XML = new XML();
			private var _xmlEntidad:XML =  new XML();
			private var _xmlMunicipio:XML =  new XML();
			private var _xmlLocalidad:XML =  new XML();
			
			private var _xmlFiltro:XMLList;
			
			public var coord:Array = new Array();
			
			public var datos:DatosAcred = new DatosAcred();
			
			private var du:Utils;
			private var global:Globales;
			public var texto:String;
			
			public function asignaEstatus(tipoPersona:String):void{
				if(tipoPersona == "F")
					currentState = "";
				else if(tipoPersona == "M")
					currentState = "moral";
			}
			
			//Realiza la busqueda de direccion del acreditado utilizando el codigo postal
			public function buscaCodPostal():void{ 
				txtCodPostal.text = StringUtil.trim(txtCodPostal.text);
				if (txtCodPostal.text != "" && txtCodPostal.length == 5){
					var wsCat:WebService = new WebService;
					var params:Array = new Array;
					
					du.initWsCat(wsCat);
					du.sCursor();
					
					_xmlDir = null;
				
					du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
						_xmlDir = new XML(evt.result.toString());
					
						limpiaDatosDir();
						
						du.rCursor();
						du.closeWs(wsCat);
						wsCat = null;
						
						if(_xmlDir.elements().length() > 0){
							comEntGrid.asignaTexto(_xmlDir.Table[0].ENTFED);
							comMunGrid.asignaTexto(_xmlDir.Table[0].MUNIC);
							comLocGrid.asignaTexto(_xmlDir.Table[0].LOC);
							comColGrid.asignaTexto(_xmlDir.Table[0].COL);
							
							datos.cdgEntFed = _xmlDir.Table[0].CDGEF;		
							datos.cdgMun = _xmlDir.Table[0].CDGMU;
							datos.cdgLoc = _xmlDir.Table[0].CDGLO;
							datos.cdgCol = _xmlDir.Table[0].CDGCOL;	
							
							obtieneMunicipio();
							obtieneLocalidad();
							obtieneColonia();
						}
					});
					params[0] = txtCodPostal.text;
					wsCat.getCatalogoGral.send(6, params);
				}
				else{
					txtCodPostal.text = "";
					obtieneMunicipio();
					obtieneLocalidad();
					obtieneColonia();
				}
			}
			
			public function copiaDatos():Object{
				var oItem:Object;
				
				oItem = new Object();
				oItem.calle = StringUtil.trim(txtCalle.text);
				oItem.noExt = StringUtil.trim(txtNumExt.text);
				oItem.noInt = StringUtil.trim(txtNumInt.text);
				oItem.entreCalles = StringUtil.trim(txtEntreCalles.text);
				oItem.cdgPais = datos.cdgPais;
				oItem.pais = comPaisGrid.obtieneDato();
				oItem.codPostal = txtCodPostal.text;
				oItem.cdgEntFed = datos.cdgEntFed;
				oItem.entFed = comEntGrid.obtieneDato();
				oItem.cdgMun = datos.cdgMun;
				oItem.munic = comMunGrid.obtieneDato();
				oItem.cdgLoc = datos.cdgLoc;
				oItem.localidad = comLocGrid.obtieneDato();
				oItem.cdgCol = datos.cdgCol;
				oItem.colonia = comColGrid.obtieneDato();
				
				return oItem;
			}
			
			public function enviarDatosAcred(formData:DatosAcred):void{
				var invalidArray:Array = Validator.validateAll([calleV, entFedV, municipioV, localidadV, coloniaV]);
				if(invalidArray.length == 0){	
					formData.calle = StringUtil.trim(txtCalle.text);
					formData.noExt = StringUtil.trim(txtNumExt.text);
					formData.noInt = StringUtil.trim(txtNumInt.text);
					formData.entreCalles = StringUtil.trim(txtEntreCalles.text);
					formData.cdgPais = datos.cdgPais;
					formData.codPostal = txtCodPostal.text;
					formData.cdgEntFed = datos.cdgEntFed;
					formData.cdgMun = datos.cdgMun;
					formData.cdgLoc = datos.cdgLoc;
					formData.cdgCol = datos.cdgCol;
					
					if(currentState == ""){
						formData.telefono = StringUtil.trim(txtTelefono.text);
					}
					formData.guardaDir = true;
				}
				else{
					formData.guardaDir = false;
				}
				var event:EventAcred = new EventAcred("enviarDatosAcred", formData);
				dispatchEvent(event);
			}
			
			public function init(info:DatosAcred, estatus:String):void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				
				global = new Globales();
				du = new Utils();
				
				currentState = estatus;
							
				du.initWsCat(wsCat);
				du.sCursor();
				
				_xmlPais = null;
				
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlPais = new XML(evt.result.toString());
					
					if(_xmlPais.elements().length() > 0){
						comPaisGrid.init(_xmlPais, 130);
						comPaisGrid.addEventListener("seleccionar",selecPais);
						comPaisGrid.addEventListener("verificar",verifPais);
					}
					
					_xmlEntidad = null;
				
					du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
						_xmlEntidad = new XML(evt.result.toString());
						
						if(_xmlEntidad.elements().length() > 0){
							comEntGrid.init(_xmlEntidad, 130);
							comEntGrid.addEventListener("seleccionar",selecEntFed);
							comEntGrid.addEventListener("verificar",verifEntFed);
						}
						
						du.rCursor();
						du.closeWs(wsCat);
						wsCat = null;
						
						if (info != null){
							txtCalle.text = info.calle;
							txtNumExt.text = info.noExt;
							txtNumInt.text = info.noInt;
							txtEntreCalles.text = info.entreCalles;
							txtCodPostal.text = info.codPostal;
							
							comPaisGrid.asignaTexto(info.pais);
							comEntGrid.asignaTexto(info.entFed);
							comMunGrid.asignaTexto(info.municipio);
							comLocGrid.asignaTexto(info.localidad);
							comColGrid.asignaTexto(info.colonia);
							
							datos.cdgPais = info.cdgPais;
							datos.cdgEntFed = info.cdgEntFed;		
							datos.cdgMun = info.cdgMun;
							datos.cdgLoc = info.cdgLoc;
							datos.cdgCol = info.cdgCol;	
							
							obtieneMunicipio();
							obtieneLocalidad();
							obtieneColonia();
							
							if(currentState == ""){
								txtTelefono.text = info.telefono;
							}
						}	
					});
					wsCat.getCatalogoGral.send(2);
				});
				wsCat.getCatalogoGral.send(1);
			}
			
			public function limpiaDatosDir():void{
				comEntGrid.limpiar();
				comMunGrid.limpiar();
				comLocGrid.limpiar();
				comColGrid.limpiar();	
				
				datos.cdgEntFed = "";		
				datos.cdgMun = "";
				datos.cdgLoc = "";
				datos.cdgCol = "";	
			}
			
			private function obtieneColonia():void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlColonia = new XML(evt.result.toString());
					
					comColGrid.init(_xmlColonia, 90);
					if(_xmlColonia.elements().length() > 0){
						comColGrid.addEventListener("seleccionar",selecColonia);
					}
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;	
				});
				params[0] = datos.cdgEntFed;
				params[1] = datos.cdgMun;
				params[2] = datos.cdgLoc;
				params[3] = txtCodPostal.text;
				wsCat.getCatalogoGral.send(5, params);
			}
			
			private function obtieneLocalidad():void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				
				du.initWsCat(wsCat);
				du.sCursor();
				
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlLocalidad = new XML(evt.result.toString());
					
					comLocGrid.init(_xmlLocalidad, 90);
					if(_xmlLocalidad.elements().length() > 0){
						comLocGrid.addEventListener("seleccionar",selecLocalidad);
						comLocGrid.addEventListener("verificar",verifLocalidad);
					}
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;
				});
				params[0] = datos.cdgEntFed;
				params[1] = datos.cdgMun;
				params[2] = txtCodPostal.text;
				wsCat.getCatalogoGral.send(4, params);
			}
			
			private function obtieneMunicipio():void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlMunicipio = new XML(evt.result.toString());
					
					comMunGrid.init(_xmlMunicipio, 130);
					if(_xmlMunicipio.elements().length() > 0){
						comMunGrid.addEventListener("seleccionar",selecMunic);
						comMunGrid.addEventListener("verificar",verifMunic);
					}
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;
				});
				params[0] = datos.cdgEntFed;
				params[1] = txtCodPostal.text;
				wsCat.getCatalogoGral.send(3, params);
			}
			
			private function selecColonia(event:Event):void{
				datos.cdgCol = event.currentTarget.codigo;
				_xmlFiltro = _xmlColonia.Table.(CODIGO.toString() == datos.cdgCol)
				if(txtCodPostal.text != _xmlFiltro.CDGPOSTAL.toString())
					txtCodPostal.text = _xmlFiltro.CDGPOSTAL.toString();
				comColGrid.setFocus();
			}
			
			private function selecEntFed(event:Event):void{
				if(datos.cdgEntFed != event.currentTarget.codigo){
					comMunGrid.limpiar();
					comLocGrid.limpiar();
					comColGrid.limpiar();
					datos.cdgEntFed = event.currentTarget.codigo;
					obtieneMunicipio();
				}
				comEntGrid.setFocus();
			}
				
			private function selecLocalidad(event:Event):void{
				if(datos.cdgLoc != event.currentTarget.codigo){
					comColGrid.limpiar();
					datos.cdgLoc = event.currentTarget.codigo;
					obtieneColonia();
				}
				comLocGrid.setFocus();
			}
				
			private function selecMunic(event:Event):void{
				if(datos.cdgMun != event.currentTarget.codigo){	
					comLocGrid.limpiar();
					comColGrid.limpiar();
					datos.cdgMun = event.currentTarget.codigo;
					obtieneLocalidad();
				}
				comMunGrid.setFocus();
			}
			
			private function selecPais(event:Event):void{
				if(datos.cdgPais != event.currentTarget.codigo){
					datos.cdgPais = event.currentTarget.codigo;
				}
				comPaisGrid.setFocus();
			}
			
			private function verifEntFed(event:Event):void{
				if(comEntGrid.codigo == ""){	
					comMunGrid.limpiar();
					comLocGrid.limpiar();
					comColGrid.limpiar();
					datos.cdgMun = "";
					datos.cdgLoc = "";
					datos.cdgCol = ""; 
				}
			}
			
			private function verifLocalidad(event:Event):void{
				if(comColGrid.codigo == ""){
					comColGrid.limpiar();
					datos.cdgCol = ""; 
				}
			}
			
			private function verifMunic(event:Event):void{
				if(comMunGrid.codigo == ""){
					comLocGrid.limpiar();
					comColGrid.limpiar();
					datos.cdgLoc = "";
					datos.cdgCol = ""; 
				}
			}
			
			private function verifPais(event:Event):void{
				datos.cdgPais = comPaisGrid.obtieneCodigo();
			}
	]]>
	</mx:Script>
	<mx:Label id="lblTitulo" text="Domicilio" x="150.5" y="12" fontSize="16" fontWeight="bold"/>
	<mx:Label id="lblCalle" text="Calle:" x="111.5" y="45"/>
	<mx:Label id="lblEntre" text="Entre las calles:" x="61.5" y="74"/>
	<mx:Label id="lblTelefono" text="Teléfono:" x="92.5" y="104"/>
	<mx:Label id="lblPais" text="País:" x="114.5" y="134"/>
	<mx:Label id="lblCodPostal" text="Código Postal:" x="69.5" y="164"/>
	<mx:Label id="lblEntFed" text="Entidad Federativa:" x="45.5" y="195"/>
	<mx:Label id="lblMunic" text="Municipio:" x="90.5" y="227"/>
	<mx:Label id="lblLoc" text="Localidad:" x="88.5" y="259"/>
	<mx:Label id="lblCol" text="Colonia:" x="99.5" y="291"/>
	<mx:TextInput id="txtCalle" maxChars="80" restrict="A-Z;a-z;Ñ;ñ;0-9; " width="170" x="150.5" y="43"/>
	<mx:TextInput id="txtEntreCalles" maxChars="50" restrict="A-Z;a-z;Ñ;ñ;0-9; " width="170" x="150.5" y="73"/>
	<mx:TextInput id="txtTelefono" restrict="0-9" maxChars="20" x="150.5" y="103"/>
	<control:TextGrid id="comPaisGrid" height="24" x="150.5" y="133"/>
	<mx:TextInput id="txtCodPostal" enter="buscaCodPostal()" width="95" maxChars="5" restrict="0-9" x="150.5" y="163"/>
	<control:TextGrid id="comEntGrid" x="150.5" height="24" y="193"/>
	<control:TextGrid id="comMunGrid" x="150.5" height="24" y="225"/>
	<control:TextGrid id="comLocGrid" x="150.5" height="24" y="257"/>
	<control:TextGrid id="comColGrid" x="150.5" height="24" y="289"/>
</mx:Canvas>