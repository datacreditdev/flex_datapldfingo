<?xml version="1.0" encoding="utf-8"?>
<mx:Form xmlns:mx="http://www.adobe.com/2006/mxml" width="420" height="420" creationPolicy="all">
	<mx:states>
		<mx:State name="moral">
			<mx:RemoveChild target="{sexo}"/>
			<mx:RemoveChild target="{entidad}"/>
			<mx:RemoveChild target="{nacionalidad}"/>
			<mx:RemoveChild target="{pais}"/>
			<mx:RemoveChild target="{curp}"/>
			<mx:RemoveChild target="{ife}"/>
			<mx:SetProperty target="{nombre}" name="label" value="Razón Social:"/>
			<mx:RemoveChild target="{txtSegNombre}"/>
			<mx:SetProperty target="{txtNombre}" name="width" value="290"/>
			<mx:RemoveChild target="{AP}"/>
			<mx:RemoveChild target="{AM}"/>
			<mx:SetProperty name="width" value="460"/>
			<mx:SetProperty target="{nombre}" name="width" value="420"/>
			<mx:AddChild position="after" relativeTo="{nombre}">
				<mx:FormItem label="Representante Legal:" id="repLegal" visible="true" width="420">
					<mx:Canvas>
						<mx:TextInput id="txtRepLegal" width="232"/>
						<mx:Button id="btnRepLegal" icon="@Embed(source='assets/ico_lupa.png')" x="240" click="mostrarFormPers()"/>
					</mx:Canvas>	
				</mx:FormItem>
			</mx:AddChild>
			<mx:SetProperty target="{fecha}" name="label" value="Fecha Constitución:"/>
			<mx:SetProperty target="{txtCodigo}" name="width" value="100"/>
		</mx:State>
	</mx:states>
	<mx:transitions>
		<mx:Transition fromState="*" toState="*">		
			<mx:Parallel>
				<mx:Fade duration="500" target="{repLegal}"/> 
			</mx:Parallel>
		</mx:Transition>
	</mx:transitions>
	
	<mx:Metadata>
		[Event(name="enviarDatosPersona", type="Data.EventPersona")]
	</mx:Metadata>
	
	<mx:Validator id="nombreV" source="{txtNombre}" property="text" 
	invalid="{txtNombre.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Acreditado Requerido"/>
	
	<mx:Validator id="repLegalV" source="{txtRepLegal}" property="text" 
	invalid="{txtRepLegal.setFocus()}" triggerEvent="" requiredFieldError="Representante Legal Requerido"/>
	 
	<mx:Validator id="aPaternoV" source="{txtAP}" property="text" 
	invalid="{txtAP.setFocus()}" triggerEvent="" requiredFieldError="Apellido Paterno Requerido"/> 
	
	<mx:Validator id="fechaV" source="{txtFecha}" property="text" 
	invalid="{txtFecha.setFocus()}" triggerEvent="" requiredFieldError="Fecha de Nacimiento Requerida"/>
	
	<mx:Validator id="rfcV" source="{txtRfc}" property="text" 
	invalid="{txtRfc.setFocus()}" triggerEvent="" requiredFieldError="RFC Requerido"/>
	
	<mx:Validator id="curpV" source="{txtCurp}" property="text" 
	invalid="{txtCurp.setFocus()}" triggerEvent="" requiredFieldError="CURP Requerida"/>
	
	<mx:Validator id="ifeV" source="{txtIfe}" property="text" 
	invalid="{txtIfe.setFocus()}" triggerEvent="" requiredFieldError="IFE Requerido"/>

	<mx:Script>
		<![CDATA[
			import Data.Permisos;
			import Data.EventPersona;
			import Data.DatosPersona;
			import Data.Utils;
			import Data.Globales;
			import mx.controls.Alert;
			import mx.validators.Validator;
			import mx.collections.ArrayCollection;
			import mx.core.Application;
			import mx.containers.TitleWindow;			
			import mx.managers.PopUpManager;
			import mx.effects.easing.Elastic;
			import mx.rpc.soap.WebService;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.xml.SimpleXMLDecoder;
			import mx.utils.ArrayUtil;
			import mx.utils.StringUtil;
			import OPERAC.MttoBusqPersona;

			private var _xmlDatos:XML =  new XML();
			private var _xmlPais:XML =  new XML();
			private var _xmlEntFed:XML =  new XML();
			
			public var paisObj:ArrayCollection = new ArrayCollection();
			public var entFedObj:ArrayCollection = new ArrayCollection();
			public var datos:DatosPersona = new DatosPersona();
			
			public var wsMS:WebService;   //variable utilizada para las llamadas asincronas de WS	
			public var du:Utils;
			private var global:Globales;
			private var permiso:Permisos;
			public var bandCURP:Boolean;
			public var f:Date;
			public var fecMin:Date;
			public var fecMax:Date;
			private var codRepLegal:String;
			
			private function agregarPers(evt:Event):void{
				var comBusqPers:MttoBusqPersona = evt.currentTarget as MttoBusqPersona;
				codRepLegal = comBusqPers.obtenerCodigo();
				txtRepLegal.text = comBusqPers.obtenerNombre();
			}
			
			public function asignaEstatus(estatus:String):void{
				if(estatus == "M")
					currentState = "moral";
				else if(estatus == "F")
					currentState = null;
			}
			
			public function initConexion():void{				
				wsMS = new WebService();			
				wsMS.wsdl = Application.application.wsStr.wsdlServ.toString();
				wsMS.loadWSDL();
			}	
			
			public function enviarDatosPersona(formData:DatosPersona):void{
				var invalidArray:Array;
				//Validacion de campos para Persona Fisica 
				if(currentState == null)
					invalidArray = Validator.validateAll([nombreV, aPaternoV, fechaV, rfcV, curpV, ifeV]);
				//Validacion de campos para Persona Moral
				else if(currentState == "moral")
					invalidArray = Validator.validateAll([nombreV, repLegalV, fechaV, rfcV]);
				if(invalidArray.length == 0){	
					formData.codigo = txtCodigo.text;
					formData.nombre = StringUtil.trim(txtNombre.text.toUpperCase());
					formData.segNombre = StringUtil.trim(txtSegNombre.text.toUpperCase());
					formData.aPaterno = StringUtil.trim(txtAP.text.toUpperCase());
					formData.aMaterno = StringUtil.trim(txtAM.text.toUpperCase());
					formData.repLegal = codRepLegal;
					formData.fecha = txtFecha.text;
					
					if (rdbMasculino.selected == true)
						formData.sexo = "M";
					if (rdbFemenino.selected == true)
						formData.sexo = "F";
					//selecciona el codigo del pais	
					if (cboPais.selectedIndex > 0)
						formData.cdgNacPais = cboPais.selectedItem.CODIGO;
					//selecciona el codigo de la entidad de nacimiento		
					if (cboEntidad.selectedIndex > 0)
						formData.cdgNacEntFed = cboEntidad.selectedItem.id;
					//selecciona el codigo de la nacionalidad
					if (cboNac.selectedIndex > 0)
					 	formData.cdgNac = cboNac.selectedItem.CODIGO;
					 									 						
					formData.rfc = txtRfc.text;
					formData.curp = txtCurp.text;
					formData.ife = StringUtil.trim(txtIfe.text); 
					
					formData.guardaDatos = true;
				}
				else
					formData.guardaDatos = false;
				var event:EventPersona = new EventPersona("enviarDatosPersona", formData);
				dispatchEvent(event);
			}
			
			public function formateaEntFed():void{
				var cont:int = _xmlEntFed.elements().length();
				var oItem:Object;
				var item:Array = new Array;
				
				oItem = new Object();
				oItem.id = "0";	
				oItem.clave = "0";
				oItem.nombre = "--Seleccionar--";
				item.push(oItem);
				
				for(var i:int = 0; i < cont; i++){
					oItem = new Object();
					oItem.id = _xmlEntFed.Table[i].CODIGO;
					oItem.clave = _xmlEntFed.Table[i].CDGCURP;	
					oItem.nombre = _xmlEntFed.Table[i].NOMBRE;
					item.push(oItem);
				}
				entFedObj = new ArrayCollection(item);
			}
			
			public function formateaPais(cadInfo:String):void{
				var oItem:Object;
				var array:Array;
				
				oItem = new Object();
				oItem.CODIGO = "0";	
				oItem.NOMBRE = "--Seleccionar--";
				oItem.GENTILICIO = "--Seleccionar--";
				
				var xml:XMLDocument = new XMLDocument(cadInfo); 
     			var decoder:SimpleXMLDecoder = new SimpleXMLDecoder();  
     			var data:Object = decoder.decodeXML(xml);  
				array = ArrayUtil.toArray(data.NewDataSet.Table);   
				paisObj = new ArrayCollection(array);
				paisObj.addItemAt(oItem, 0);
			}
			
			public function generaCURP():void{
				if(currentState == null){
					//Elimina espacios al principio y final de los textos 
					txtAP.text = StringUtil.trim(txtAP.text);
					txtAM.text = StringUtil.trim(txtAM.text);
					txtNombre.text = StringUtil.trim(txtNombre.text);
					txtSegNombre.text = StringUtil.trim(txtSegNombre.text);
					
					if (txtAP.text != "" && txtNombre.text != "" && txtFecha.text != "" 
					&& (rdbMasculino.selected == true || rdbFemenino.selected == true) && cboEntidad.selectedIndex > 0){
							var wsCat:WebService = new WebService;
							var params:Array = new Array;
							var curp:String;
							var nombres:String;
							var sexo:String;
							var clave:String;
							var j:int;
												
							du.initWsCat(wsCat);
							du.sCursor();
							
							nombres = txtNombre.text + " " + txtSegNombre.text;
							
							if(rdbMasculino.selected == true) 
								sexo = "H";
							else if(rdbFemenino.selected == true)
								sexo = "M";
							
							for(j = 0; j < entFedObj.length; j++){
								if (entFedObj[j].nombre.toString() == cboEntidad.text){
									clave = entFedObj[j].clave.toString();	
									break;		
								}			
							}
								
							du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
								curp = evt.result.toString();
		
								du.rCursor();
								du.closeWs(wsCat);
								
								wsCat = null;
								
								txtRfc.text = curp.substr(0,10);
								txtCurp.text = curp;	
							});
							//GENERA EL CURP DEL CLIENTE
							params[0] = txtAP.text;
							params[1] = txtAM.text;
							params[2] = nombres;
							params[3] = txtFecha.text;
							params[4] = sexo;
							params[5] = clave;
							wsCat.getInfoAcred.send(2, params);
					}
					else{
						txtRfc.text = "";
						txtCurp.text = "";
					}	
				}
			}
			
			public function init(info:DatosPersona):void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				
				du = new Utils();
				global = new Globales();
				permiso = new Permisos();
				
				f = Application.application._Current_Fecha;
				
				permisos();
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlPais = new XML(evt.result.toString());
					
					if (_xmlPais.elements().length() > 0){
						formateaPais(evt.result.toString());
						cboPais.dataProvider = paisObj;
						cboNac.dataProvider = paisObj;
					}
 	
					du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
						_xmlEntFed = new XML(evt.result.toString());
						
						du.rCursor();
						du.closeWs(wsCat);
						Application.application.desbloquear();						
						
						if (_xmlEntFed.elements().length() > 0){
							formateaEntFed();
							cboEntidad.dataProvider = entFedObj;
						}
					
						if (info != null){
							var j:int;
							txtCodigo.text = info.codigo;
							txtNombre.text = info.nombre;
							txtSegNombre.text = info.segNombre;
							txtAP.text = info.aPaterno;
							txtAM.text = info.aMaterno;
							//Informacion relacionada con el representante legal
							if(currentState == "moral"){
								codRepLegal = info.repLegal;
								txtRepLegal.text =  info.nomRepLegal;
							}
							if (info.sexo.toUpperCase() == "F")
								rdbFemenino.selected = true;
							else if (info.sexo.toUpperCase() == "M")
								rdbMasculino.selected = true;
							txtFecha.text = info.fecha;
							//selecciona el pais del acreditado
							for(j = 0; j < paisObj.length; j++){   
								if (paisObj[j].CODIGO.toString() == info.cdgNacPais){
									cboPais.selectedIndex = j;
									break;
								}
							}
							//selecciona la entidad federativa del acreditado
							for(j = 0; j < entFedObj.length; j++){   
								if (entFedObj[j].id.toString() == info.cdgNacEntFed){
									cboEntidad.selectedIndex = j;
									break;
								}
							} 
							//selecciona la nacionalidad del acreditado
							for(j = 0; j < paisObj.length; j++){   
								if (paisObj[j].CODIGO.toString() == info.cdgNac){
									cboNac.selectedIndex = j;
									break;
								}
							}
							txtRfc.text = info.rfc;
							txtCurp.text = info.curp;
							txtIfe.text = info.ife;
						}		
						else{
							for(j = 0; j < paisObj.length; j++){   
								//cboPais.selectedIndex = paisObj.getItemIndex("MEXICO");
								if (paisObj[j].NOMBRE.toString().toUpperCase() == "MEXICO"){
									cboPais.selectedIndex = j;
									break;
								}
							}
							for(j = 0; j < paisObj.length; j++){   
								if (paisObj[j].GENTILICIO.toString().toUpperCase() == "MEXICANO"){
									cboNac.selectedIndex = j;
									break;
								}
							}
							
							/*initConexion();
							du.sCursor();
					
							du.ejecutaWsMethod(wsMS,function(evt:ResultEvent):void {											
								var codigo:String = evt.result.toString();
								
								txtCodigo.text = codigo;
								
								Application.application.desbloquear();
								du.rCursor();
								du.closeWs(wsMS);
							});
							//EXTRAE EL CODIGO CONSECUTIVO DE PERSONAS
							wsMS.setCodigoPersona.send();*/
						}
					});
					//MUESTRA LAS ENTIDADES FEDERATIVAS DISPONIBLES
					wsCat.getCatalogoGral.send(2);
				});
				//CONSULTA LOS PAISES DISPONIBLES
				wsCat.getCatalogoGral.send(1);
			}
			
			private function mostrarFormPers():void{
				var comBusqPers:MttoBusqPersona = new MttoBusqPersona;
				comBusqPers = MttoBusqPersona(PopUpManager.createPopUp(this,MttoBusqPersona,true));
				comBusqPers.addEventListener("enviar", agregarPers); 
				PopUpManager.centerPopUp(comBusqPers);
			}
			
			public function permisos():void{
				if(permiso.permisosModAcred(Application.application.PERFIL_ID)){
					txtRfc.editable = true;
					txtCurp.editable = true;
				}
			}
	]]>
	</mx:Script>
	<mx:FormItem label="Código:" id="codigo" visible="true">
		<mx:TextInput id="txtCodigo" maxChars="12" editable="false"/>
	</mx:FormItem>
	<mx:FormItem label="Nombres:" id="nombre" visible="true" width="384">
		<mx:Canvas>
			<mx:TextInput id="txtNombre" width="129" restrict="A-Z;a-z;Ñ;ñ; " focusOut="generaCURP()"/>
			<mx:TextInput id="txtSegNombre" x="127" width="137" restrict="A-Z;a-z;Ñ;ñ; " focusOut="generaCURP()"/>	
		</mx:Canvas>
	</mx:FormItem>
	<mx:FormItem label="Apellido Paterno:" id="AP" visible="true">
		<mx:TextInput id="txtAP" restrict="A-Z;a-z;Ñ;ñ; " focusOut="generaCURP()"/>
	</mx:FormItem>
	<mx:FormItem label="Apellido Materno:" id="AM" visible="true">
		<mx:TextInput id="txtAM" restrict="A-Z;a-z;Ñ;ñ; " focusOut="generaCURP()"/>
	</mx:FormItem>
	<mx:FormItem label="Genero:" id="sexo" visible="true" width="330">
		<mx:Canvas>
			<mx:RadioButton id="rdbFemenino" label="Femenino" focusOut="generaCURP()"/>
			<mx:RadioButton id="rdbMasculino" label="Masculino" x="80" focusOut="generaCURP()"/>	
		</mx:Canvas>	
	</mx:FormItem>
	<mx:FormItem label="Fecha Nacimiento:" id="fecha" visible="true" width="280">
		<mx:DateField id="txtFecha" width="109" yearNavigationEnabled="true" focusOut="generaCURP()"/>
	</mx:FormItem>
	<mx:FormItem label="País Nacimiento:" id="pais" visible="true" width="290">
		<mx:ComboBox id="cboPais" labelField="NOMBRE" width="170"/>
	</mx:FormItem>
	<mx:FormItem label="Entidad Nacimiento:" id="entidad" visible="true" width="290">
		<mx:ComboBox id="cboEntidad" labelField="nombre" width="170" focusOut="generaCURP()"/>
	</mx:FormItem>
	<mx:FormItem label="Nacionalidad:" id="nacionalidad" visible="true" width="290">
		<mx:ComboBox id="cboNac" labelField="GENTILICIO" width="170"/>
	</mx:FormItem>
	<mx:FormItem label="RFC:" id="rfc" visible="true">
		<mx:TextInput id="txtRfc" focusIn="generaCURP()" editable="false"/>
	</mx:FormItem>
	<mx:FormItem label="CURP:" id="curp" visible="true">
		<mx:TextInput id="txtCurp" focusIn="generaCURP()" editable="false"/>
	</mx:FormItem>
	<mx:FormItem label="IFE:" id="ife" visible="true">
		<mx:TextInput id="txtIfe" restrict="0-9" maxChars="15"/>
	</mx:FormItem>
</mx:Form>