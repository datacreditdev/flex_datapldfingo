<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="initApp()" width="100%" xmlns:forms="forms.*" 
	xmlns:Data="Data.*" horizontalScrollPolicy="off" creationPolicy="all" xmlns:Administracion="Administracion.*" 
	height="100%">
	
	<!--Componente de Registro de Ajustes-->
	<mx:Script>
		<![CDATA[
			import Data.Globales;
			import Data.Utils;
			import mx.collections.ArrayCollection;
			import mx.containers.TitleWindow;
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.effects.easing.Elastic;
			import mx.events.CloseEvent;	
			import mx.events.DataGridEvent;				
			import mx.events.FlexEvent;
			import mx.events.ListEvent;	
			import mx.events.ValidationResultEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.Fault;
			import mx.rpc.soap.WebService;
			import mx.rpc.xml.SimpleXMLDecoder;
			import mx.utils.ArrayUtil;  
			          
			public var _xmlInfo:XML = new XML();
			public var _xmlJust:XML = new XML();
			
			private var wsMS:WebService;	
			private var titulo:String;
			private var codigo:String;
			private var tipoCred:String;
			private var ciclo:String;
			private var saldoCap:Number;
			private var saldoInt:Number;
			private var saldoMora:Number;
			private var total:Number;
			private var du:Utils;
			private var global:Globales;
			private var vResult:ValidationResultEvent;
		
			public var justObj:ArrayCollection = new ArrayCollection();

			public function buscarInfo():void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				if (txtCodigo.text != "" && txtCiclo.text != ""){
					codigo = txtCodigo.text;
					ciclo = txtCiclo.text;
					btnBuscar.setFocus();
					
					du.initWsCat(wsCat);
					du.sCursor();
					global.bloquear();
							
					du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {																				
						_xmlInfo = new XML(evt.result);
						
						du.rCursor();
						du.closeWs(wsCat);
						global.desbloquear();
				
						if (_xmlInfo.elements().length() > 0){
							saldoCap = Number(_xmlInfo.Table[0].CAPITAL);
							saldoInt = Number(_xmlInfo.Table[0].INTERES);
							saldoMora = Number(_xmlInfo.Table[0].MORATORIOS);
							total = saldoCap + saldoInt + saldoMora;
							lblSdoCapital.text = global.formatoMoneda(saldoCap.toString());
							lblSdoInteres.text = global.formatoMoneda(saldoInt.toString());
							lblSdoMora.text = global.formatoMoneda(saldoMora.toString());
							lblSdoTotal.text = global.formatoMoneda(total.toString());
							calculaNuevoSaldo();
						}		
						else
							Alert.show("No se ha encontrado información.\n\nVerifique los datos capturados.",titulo,4,null,null,global.iAlert);
					});
					params[0] = codigo;
					params[1] = tipoCred;
					params[2] = ciclo;
					wsCat.getInfoGeneral.send(10, params);
				}	
				else
					Alert.show("Debe capturar el código y ciclo.",titulo,4,null,null,global.iAlert);
			}
			
			public function calculaNuevoSaldo():void{
				var nvoCap:Number;
				var nvoInt:Number;
				var nvoMora:Number;
				var nvoTotal:Number;
				nvoCap = saldoCap + Number((txtCapital.text == "" ? "0": txtCapital.text));
				nvoInt = saldoInt + Number((txtInteres.text == "" ? "0": txtInteres.text));
				nvoMora = saldoMora + Number((txtMora.text == "" ? "0": txtMora.text));
				nvoTotal = nvoCap + nvoInt + nvoMora; 
				lblNvoSdoCap.text = global.formatoMoneda(nvoCap.toString());
				lblNvoSdoInt.text = global.formatoMoneda(nvoInt.toString());
				lblNvoSdoMora.text = global.formatoMoneda(nvoMora.toString());
				lblNvoSdoTotal.text = global.formatoMoneda(nvoTotal.toString());
			}
			
			public function enviar():void{
				if(valida()){
					var capital:Number;
					var interes:Number;
					var moratorios:Number;
					var fecha:String;
					var justifica:String;
					var observaciones:String;
					
					capital = Number((txtCapital.text == "" ? "0": txtCapital.text));
					interes = Number((txtInteres.text == "" ? "0": txtInteres.text));
					moratorios = Number((txtMora.text == "" ? "0": txtMora.text));
					fecha = dtfFecha.text;
					justifica = cboJustifica.selectedItem.id;
					observaciones = txtObs.text;
					
					initConexion();
					du.sCursor();
							
					du.ejecutaWsMethod(wsMS,function(evt:ResultEvent):void {																				
						var res:String = evt.result.toString();
						
						du.rCursor();
						du.closeWs(wsMS);
							
						if(res.substr(0,1) == "1"){	
							Alert.show("Proceso finalizado exitosamente.",titulo,4,null,null,global.iInfo);
							limpiar();
						}
						else
							Alert.show(res.substr(2,res.length-1),titulo,4,null,null,global.iAlert);
					}); 
					wsMS.setRegistroAjuste.send(codigo, ciclo, tipoCred, fecha, capital, interes, moratorios, justifica, observaciones, 
												global.obtenerUsuario());
				}
			}
			
			private function formateaJustifica():void{
				var cont:int = _xmlJust.elements().length();
				var item:Array = new Array;
				var oItem:Object;
				
				justObj.removeAll();
				justObj.refresh();
				
				oItem = new Object();
				oItem.id = "0";	
				oItem.descripcion = "--Seleccionar--";
				item.push(oItem);
				
				for (var i:int = 0; i < cont; i++){
					oItem = new Object();
					oItem.id = _xmlJust.Table[i].CODIGO;
					oItem.descripcion = _xmlJust.Table[i].DESCRIPCION;		
					item.push(oItem);
				}
				justObj = new ArrayCollection(item);
			}
			
			public function initApp():void{
				var wsCat:WebService = new WebService;
				var params:Array = new Array;
				global = new Globales();
				du = new Utils();
				titulo = "Registro de Ajustes";
				tipoCred = "I";
				lblTitulo.text = titulo.toUpperCase();
				limpiar();
				
				du.initWsCat(wsCat);
				du.sCursor();
				
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlJust = new XML(evt.result);
					
					du.rCursor();
					du.closeWs(wsCat);
					
					if (_xmlJust.elements().length() > 0){
						formateaJustifica();
						cboJustifica.dataProvider = justObj;
					}
					else{
						Alert.show("Error en la carga de Datos",titulo,4,null,null,global.iAlert);
					}
				});
				params[0] = "D";
				//Obtiene Justificaciones de Ajuste
				wsCat.getCatalogoGral.send(21, params);
			}
			
			public function initConexion():void{				
				wsMS = new WebService();			
				wsMS.wsdl = Application.application.wsStr.wsdlServ.toString();
				wsMS.loadWSDL();
			}			
			
			private function limpiar():void{
				var fec:Date = global.obtenerFechaSistema();
				dtfFecha.selectedDate = global.seleccionaDiaHabil(new Date(fec.fullYear,fec.month,fec.date));
				saldoCap = 0;
				saldoInt = 0;
				txtCodigo.text = "";
				txtCiclo.text = "";
				lblSdoCapital.text = "";
				lblSdoInteres.text = "";
				lblSdoMora.text = "";
				lblSdoTotal.text = "";
				lblNvoSdoCap.text = "";
				lblNvoSdoInt.text = "";
				lblNvoSdoMora.text = "";
				lblNvoSdoTotal.text = "";
				cboJustifica.selectedIndex = 0;
				txtCapital.text = "";
				txtInteres.text = "";
				txtMora.text = "";
				txtObs.text = "";
			}
			
			public function valida():Boolean{
				if(dtfFecha.text == ""){
					Alert.show("Debe capturar la fecha del ajuste.",titulo,4,null,null,global.iAlert);
					return false;
				}
				if((txtCapital.text == "" || txtCapital.text == "-" || Number(txtCapital.text) == 0) &&
				   (txtInteres.text == "" || txtInteres.text == "-" || Number(txtInteres.text) == 0) &&
				   (txtMora.text == "" || txtMora.text == "-" || Number(txtMora.text) == 0)){
					Alert.show("Debe capturar información válida al menos en alguno de los campos correspondientes.",titulo,4,null,null,global.iAlert);
					return false;
				}
				if(cboJustifica.selectedIndex <= 0){
					Alert.show("Debe seleccionar alguna opción de justificación de los registros.",titulo,4,null,null,global.iAlert);
					return false;
				}
				return true;
			}
			
			public function validaMonto(event:Event):void{
				if(TextInput(event.currentTarget).text != "-"){
					numVal.source = TextInput(event.currentTarget);
					vResult = numVal.validate();
			
					if(vResult.type!=ValidationResultEvent.VALID)
			           	TextInput(event.currentTarget).text = "";
			    }
			}
		]]>
	</mx:Script>
	
	<mx:NumberValidator id="numVal" property="text" precision="2" allowNegative="true"   
		 decimalSeparator="." thousandsSeparator="," domain="real" required="false"/>
	
	<mx:Canvas backgroundAlpha="1.0" backgroundColor="#FFFFFF" width="490" height="500">
		<mx:Canvas id="cnvDetalle" x="10" y="92" width="470" height="368" styleName="canvasMod" visible="true" enabled="true">
			<mx:Label id="lblFecha" x="75.8" y="91" text="Fecha:" width="50" textAlign="right"/>
			<mx:Label id="lblCapital" x="85.8" y="120" text="Capital:" textAlign="right"/>
			<mx:Label id="lblInteres" x="300.3" y="120" text="Interes:" textAlign="right"/>
			<mx:Label id="lblDevolucion" x="50.65" y="182" text="Justificación:" width="75.15" textAlign="right"/>
			<mx:Label id="lblSdoInteres" x="327.3" y="16" width="90"/>
			<mx:Label id="lblSdoCapital" x="138.8" y="17" width="90"/>
			<mx:Label id="lblESdoCapital" x="60.8" y="17" text="Saldo Capital:" textAlign="right"/>
			<mx:Label id="lblESdoInteres" x="248.3" y="16" text="Saldo Interes:" textAlign="right"/>
			<mx:Label id="lblSdoTotal" x="327.3" y="45" width="110"/>
			<mx:Label id="lblESdoTotal" x="258.3" y="45" text="Saldo Total:" textAlign="right"/>
			<mx:HRule x="10" y="79" width="448"/>
			<mx:Label id="lblNvoSdoInt" x="355.3" y="304" width="100"/>
			<mx:Label id="lblNvoSdoCap" x="139.2" y="302" width="90"/>
			<mx:Label id="lblENvoSdoCap" x="27.2" y="302" text="Nuevo Saldo Capital:" textAlign="right"/>
			<mx:Label id="lblENvoSdoInt" x="242.3" y="304" text="Nuevo Saldo Interes:" textAlign="right"/>
			<mx:Label id="lblNvoSdoTotal" x="355.3" y="332" width="100"/>
			<mx:Label id="lblENvoSdoTotal" x="252.3" y="332" text="Nuevo Saldo Total:" textAlign="right"/>
			<mx:HRule x="10" y="291" width="448"/>
			<mx:Label id="lblObs" x="43.8" y="212" text="Observaciones:" textAlign="right"/>
			<mx:DateField id="dtfFecha" x="133.8" y="89" width="100"/>
			<mx:TextInput id="txtCapital" x="133.8" y="119" width="100" maxChars="9" restrict="0-9;.;\-" change="validaMonto(event)" focusOut="calculaNuevoSaldo()"/>
			<mx:TextInput id="txtInteres" x="349.3" y="119" width="100" maxChars="9" restrict="0-9;.;\-" change="validaMonto(event)" focusOut="calculaNuevoSaldo()"/>
			<mx:ComboBox id="cboJustifica" labelField="descripcion" x="133.8" y="179" width="315.5"/>
			<mx:TextArea id="txtObs" x="133.8" y="211" width="315.5" height="70" maxChars="256" restrict="A-Z;a-z;ñ;Ñ; ;"/>
			<mx:Label id="lblSdoMora" x="138.8" y="45" width="90"/>
			<mx:Label id="lblESdoMora" x="41.8" y="45" text="Saldo Moratorios:" textAlign="right"/>
			<mx:Label id="lblMora" x="66.8" y="150" text="Moratorios:" textAlign="right"/>
			<mx:TextInput id="txtMora" x="133.8" y="149" width="100" maxChars="9" restrict="0-9;.;\-" change="validaMonto(event)" focusOut="calculaNuevoSaldo()"/>
			<mx:Label id="lblNvoSdoMora" x="139.2" y="330" width="90"/>
			<mx:Label id="lblENvoSdoMora" x="8.2" y="330" text="Nuevo Saldo Moratorios:" textAlign="right"/>
		</mx:Canvas>
		<mx:Label id="lblTitulo" x="10" y="10" width="380.5" fontSize="12" fontWeight="bold" fontStyle="italic"/>
		<mx:Canvas id="cnvEncabezado" x="28" y="39" width="434" height="45" styleName="canvasMod">
			<mx:Label id="lblCodigo" x="51.7" y="12" text="Código:" width="50" textAlign="right"/>
			<mx:Label id="lblCiclo" x="185.7" y="12" text="Ciclo:" width="45" textAlign="right"/>
			<mx:TextInput id="txtCodigo" x="109.7" y="10" width="68" enter="buscarInfo()" maxChars="6" restrict="0-9"/>
			<mx:TextInput id="txtCiclo" x="238.7" y="10" width="50" enter="buscarInfo()" maxChars="6" restrict="0-9"/>
			<mx:Button id="btnBuscar" x="318.25" y="10" icon="@Embed(source='assets/ico_lupa.png')" click="buscarInfo()" toolTip="Buscar Grupo" />
		</mx:Canvas>
		<mx:Button id="btnAceptar" x="400" y="468" icon="@Embed(source='assets/button_ok.png')" click="enviar()" enabled="true" toolTip="Aceptar" width="80"/>
	</mx:Canvas>
</mx:Canvas>